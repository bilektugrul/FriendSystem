command /friend [<text=help>] [<offlineplayer>] [<text>]:
	aliases: /fr, /friends, /buddy
	executable by: players
	trigger:
		if arg-1 is "help":
			if yaml list "help-command" from "FriendSystem" is set:
				if arg-2 is set:
					if arg-2 isn't "all":
						set {_helpCmd::*} to page "%arg-2%" parsed as number of yaml list "help-command" from "FriendSystem" with 4 lines
						if {_helpCmd::*} is set:
							set {_nextPage} to "%arg-2%" parsed as number + 1
							set {_nextPageMsg} to yaml value "next-page-msg" from "FriendSystem"
							replace "%%page%%" with "%arg-2%" in {_nextPageMsg}
							replace "%%next%%" with "%{_nextPage}%" in {_nextPageMsg}
							send "%{_nextPageMsg}%" and "%nl% "
							send "%join {_helpCmd::*} with nl%" and "%nl% "
							send "%{_nextPageMsg}%"
						else:
							send "%{friendSystem::messagePrefix}% &cInvalid page."
					else:
						set {_helpCmd::*} to yaml list "help-command" from "FriendSystem"
						send "%yaml value ""message-splitter"" from ""FriendSystem""%"
						send "%join {_helpCmd::*} with nl%"
						send "%yaml value ""message-splitter"" from ""FriendSystem""%"
				else:
					execute player command "fr help 1"	
			else:
				send "&cThere are some errors. Please contact with this server's owner."
				send "&3FriendSystem &b| &cSystem can't find YAML list ""help-command"", please reload system." to console
		else if arg-1 is "reload":
			if player has permission "friendsystem.admin":
				unload yaml "FriendSystem"
				load yaml "plugins/FriendSystem/messages.yml" as "FriendSystem"
				if yaml "FriendSystem" is empty:
					send "&cFriendsSystem's config is empty, please reload the FR-Main.sk"
				else:
					send "%{friendSystem::messagePrefix}% &6Config reloaded."
			else:
				set {_perm-msg} to yaml value "no-permission" from "FriendSystem"
				replace "%%prefix%%" with "%{friendSystem::messagePrefix}%" in {_perm-msg}
				send {_perm-msg}
		else if arg-1 is "add":
			if arg-2 is set:
				if arg-2 isn't player:
					if {friendSystem::%player%::sentRequests::%arg-2%} isn't set:
						if {friendSystem::%arg-2%::sentRequests::%player%} isn't set:
							if {friendSystem::%player%::friends::%arg-2%} isn't set:
								if {friendSystem::%arg-2%::blockedPlayers::%player%} isn't set:
									if {friendSystem::%player%::blockedPlayers::%arg-2%} isn't set:
										if {friendSystem::%arg-2%::settings::friendRequests} isn't set:
											set {friendSystem::%player%::sentRequests::%arg-2%} to arg-2
											set {friendSystem::%arg-2%::friendRequests::%player%} to player
											send "%{friendSystem::messagePrefix}% &6You sent a friend request to player &f%arg-2%&6." and "%{friendSystem::messagePrefix}% &6Click &F<cmd:/fr cancel %arg-2%>&nhere&r &6to cancel the request."
											send "%{friendSystem::messagePrefix}% &6Player &f%player% &6sent a friend request to you!" to arg-2
											send "%{friendSystem::messagePrefix}% &6Click <cmd:/fr accept %player%>&l&nthis&r &6to accept request!&r" and "%{friendSystem::messagePrefix}% &6Click &l&n<cmd:/fr deny %player%>this&r &6to refuse request!" to arg-2
										else:
											send "%{friendSystem::messagePrefix}% &cPlayer you want to send request, doesn't accept requests."
									else:
										send "%{friendSystem::messagePrefix}% &cYou was blocked this player."
								else:
									send "%{friendSystem::messagePrefix}% &cYou can't send friend request to this player."
							else:
								send "%{friendSystem::messagePrefix}% &cThis player is already your friend."
						else:
							send "%{friendSystem::messagePrefix}% &cYou can't send friend request to this player because this player already sent a friend request to you."
					else:
						send "%{friendSystem::messagePrefix}% &cYou can't send friend request to this player because you already sent a friend request to this player."
				else:
					send "%{friendSystem::messagePrefix}% &cYou can't send friend request to yourself."
			else:
				send "%{friendSystem::messagePrefix}% &c/friend add <player>"
		else if arg-1 is "cancel":
			if arg-2 is set:
				if {friendSystem::%player%::sentRequests::%arg-2%} is set:
					delete {friendSystem::%player%::sentRequests::%arg-2%}
					delete {friendSystem::%arg-2%::friendRequests::%player%}
					send "%{friendSystem::messagePrefix}% &6You have canceled the request."
					send "%{friendSystem::messagePrefix}% &f%player% &6has canceled his friendship request." to arg-2
				else:
					send "%{friendSystem::messagePrefix}% &cYou doesn't sent friend request to this player."
			else:
				send "%{friendSystem::messagePrefix}% &c/friend cancel <player>"
		else if arg-1 is "accept":
			if arg-2 is set:
				if {friendSystem::%player%::friendRequests::%arg-2%} is set:
					delete {friendSystem::%player%::friendRequests::%arg-2%}
					delete {friendSystem::%arg-2%::sentRequests::%player%}
					delete {friendSystem::%player%::requestAccept}
					set {friendSystem::%player%::friends::%arg-2%} to arg-2
					set {friendSystem::%arg-2%::friends::%player%} to player
					send "%{friendSystem::messagePrefix}% &6You have accepted &f%arg-2%'s &6friend request!"
					send "%{friendSystem::messagePrefix}% &f%player% &6have accepted your friend request." to arg-2
				else:
					send "%{friendSystem::messagePrefix}% &cPlayer &f%arg-2% &chasn't sent you a friend request."
			else:
				send "%{friendSystem::messagePrefix}% &c/friend accept <player>"
		else if arg-1 is "deny":
			if arg-2 is set:
				if {friendSystem::%player%::friendRequests::%arg-2%} is set:
					delete {friendSystem::%player%::friendRequests::%arg-2%}
					delete {friendSystem::%arg-2%::sentRequests::%player%}
					delete {friendSystem::%player%::requestAccept}
					send "%{friendSystem::messagePrefix}% &6You have refused &f%arg-2%'s &6friend request."
					send "%{friendSystem::messagePrefix}% &f%player% &6have refused your friend request." to arg-2
				else:
					send "%{friendSystem::messagePrefix}% &cPlayer &f%arg-2% &chasn't sent you a friend request."
			else:
				send "%{friendSystem::messagePrefix}% &c/friend deny <player>"
		else if arg-1 is "remove" or "rem":
			if arg-2 is set:
				if {friendSystem::%player%::friends::%arg-2%} is set:
					delete {friendSystem::%player%::friends::%arg-2%}
					delete {friendSystem::%arg-2%::friends::%player%}
					send "%{friendSystem::messagePrefix}% &6Player &f%player% &6removed you from own friend list." to arg-2
					send "%{friendSystem::messagePrefix}% &6You have removed &f%arg-2% &6from your friend list."
				else:
					send "%{friendSystem::messagePrefix}% &f%arg-2% &cisn't your friend."
			else:
				send "%{friendSystem::messagePrefix}% &c/friend remove <player>"
		else if arg-1 is "list":
			if arg-2 isn't set:
				open virtual chest inventory with 1 rows named "&eFriends / Requests Menu" to player
				make gui slot 3 of player with skeleton head named "&6Friends" with lore "&3You have &f%size of {friendSystem::%player%::friends::*}% &3friend." to close then run function frOpenGUI(player, "friends", 1)
				make gui slot 5 of player with zombie head named "&6Requests" with lore "&3You have &f%size of {friendSystem::%player%::friendRequests::*}% &3friend request." to close then run function frOpenGUI(player, "requests", 1)
				make gui slot 8 of player with redstone torch on named "&cOptions" to close then execute player command "fr options"
			else if arg-2 is "friends":
				frOpenGUI(player, "friends", 1)
			else if arg-2 is "requests":
				frOpenGUI(player, "requests", 1)
		else if arg-1 is "msg":
			if arg-2 is set:
				if {friendSystem::%player%::friends::%arg-2%} is set:
					if arg-2 is online:
						if arg-3 is set:
							send "%{friendSystem::messagePrefix}% <suggest command:/fr msg %player%>&f&n%player%&r &6-> &fYou: &b%uncolored arg-3%" to arg-2
							send "%{friendSystem::messagePrefix}% &fYou &6-> <suggest command:/fr msg %arg-2%>&f&n%arg-2%&r: &b%uncolored arg-3%"
						else:
							set {friendSystem::%player%::regularChat} to arg-2
							send "%{friendSystem::messagePrefix}% &6Every message you write will go to &f%arg-2% &6until you close the feature." and "%{friendSystem::messagePrefix}% &6For quit from regular chat, write &fleave &6to chat."
					else:
						send "%{friendSystem::messagePrefix}% &cThis friend isn't online."
				else:
					send "%{friendSystem::messagePrefix}% &cThis player isn't your friend.%nl%%{friendSystem::messagePrefix}% &cFor see your friend list, type &f<suggest command:/fr list>/fr list&r&c."
			else:
				send "%{friendSystem::messagePrefix}% &c/friend msg <player> [<message>]"
		else if arg-1 is "bc" or "broadcast":
			if {friendSystem::%player%::friends::*} is set:
				if arg-2 is set:
					send "%{friendSystem::messagePrefix}% <suggest command:/fr msg %player%>&f%player%&r's &6broadcast:" and "%{friendSystem::messagePrefix}% &b%arg-2% %arg-3 otherwise """"%" to player and {friendSystem::%player%::friends::*} 
				else:
					send "%{friendSystem::messagePrefix}% &cYou must enter a broadcast message."
			else:
				send "%{friendSystem::messagePrefix}% &cYou don't have any friend."
		else if arg-1 is "options":
			frOpenGUI(player, "options", 1)
		else if arg-1 is "block":
			if arg-2 is set:
				if arg-2 isn't player:
					if {friendSystem::%player%::blockedPlayers::%arg-2%} isn't set:
						set {friendSystem::%player%::blockedPlayers::%arg-2%} to arg-2
						send "%{friendSystem::messagePrefix}% &6You blocked &f%arg-2%." 
						if {friendSystem::%player%::friends::%arg-2%} is set:
							delete {friendSystem::%player%::friends::%arg-2%}
							delete {friendSystem::%arg-2%::friends::%player%}
							send "%{friendSystem::messagePrefix}% &6System removed &f%arg-2% &6from your friends list because you blocked &f%arg-2%."
					else:
						delete {friendSystem::%player%::blockedPlayers::%arg-2%}
						send "%{friendSystem::messagePrefix}% &6You removed &f%arg-2% &6from your blocked players list."
				else:
					send "%{friendSystem::messagePrefix}% &cYou can't block yourself."
			else:
				send "%{friendSystem::messagePrefix}% &c/fr block <player>"
		else:
			send "%{friendSystem::messagePrefix}% &cYou used a wrong command. For command list, please type &f&n<suggest command:/fr help all>/fr help all&r."
on chat:
	if {friendSystem::%player%::regularChat} is set:
		cancel event
		if {friendSystem::%player%::regularChat} is online:
			if message isn't "leave":
				send "%{friendSystem::messagePrefix}% <suggest command:/fr msg %player%>&f&n%player%&r &6-> &fYou: &b%uncolored message%" to {friendSystem::%player%::regularChat}
				send "%{friendSystem::messagePrefix}% &fYou &6-> <suggest command:/fr msg %{friendSystem::%player%::regularChat}%>&f&n%{friendSystem::%player%::regularChat}%&r: &b%uncolored message%"
			else:	
				delete {friendSystem::%player%::regularChat}
				send "%{friendSystem::messagePrefix}% &cYou leaved from regular chat."
		else:
			send "%{friendSystem::messagePrefix}% &f%{friendSystem::%player%::regularChat}% &cisn't online.%nl%%{friendSystem::messagePrefix}% &cYou leaving from private chat."
			delete {friendSystem::%player%::regularChat}
on quit:
	if {friendSystem::%player%::settings::lastSeen} isn't set:
		set {friendSystem::%player%::lastSeen} to now
	else:
		set {friendSystem::%player%::lastSeen} to "&cUnknown"
	delete {friendSystem::%player%::requestAccept}
on inventory close:
	if event-inventory's name is "&8Friend request (&e%{friendSystem::%player%::requestAccept}%&8)":
		delete {friendSystem::%player%::requestAccept}
function frOpenGUI(p: player, s: text, pageno: number):
	if {_s} is "friends":
		open virtual chest inventory with size 6 named "&8Your friends" to {_p}
		set {_page} to {_pageno}
		set {_%{_s}%::*} to page {_pageno} of {friendSystem::%{_p}%::friends::*} with 44 lines 
		set {_slot} to 0
		loop {_%{_s}%::*}:
			set {_loop-value} to loop-value
			if loop-value is online:
				make gui slot {_slot} of {_p} with skull of "%loop-value%" parsed as offlineplayer named "&3%loop-value%" with lore "||&aPosition: &f&lOnline||&6Right click to delete from friends.||&f" to run:
					if gui-click-type is rmb:
						close {_p}'s inventory
						execute {_p} command "fr remove %{_loop-value}%"
			else:
				make gui slot {_slot} of {_p} with skull of "%loop-value%" parsed as offlineplayer named "&c%loop-value%" with lore "||&aPosition: &f&lOffline||&aLast seen: &f&l%{friendSystem::%loop-value%::lastSeen}%||&6Right click to delete from friends.||&f" to run:
					if gui-click-type is rmb:
						close {_p}'s inventory
						execute {_p} command "fr remove %{_loop-value}%"
			add 1 to {_slot}
		make gui slot 50 of {_p} with skull of "MHF_ArrowRight" parsed as offlineplayer named "&6Go forward" to run:
			add 1 to {_page}
			frOpenGUI({_p}, "%{_s}%", {_page})
		if {_page} is greater than 1:
			make gui slot 48 of {_p} with skull of "MHF_ArrowLeft" parsed as offlineplayer named "&6Go back" to run:
				remove 1 from {_page}
				frOpenGUI({_p}, "%{_s}%", {_page})
		make gui slot 49 of {_p} with book named "&e&lPage %{_page}%"
		make gui slot 53 of {_p} with magma cream named "&e&lGo To Main Page" to run:
			execute {_p} command "fr list"
		make gui slot 45 of {_p} with magma cream named "&e&lGo To First Page" to run:
			set {_page} to 1
			frOpenGUI({_p}, "%{_s}%", {_page})
	else if {_s} is "requests":
		open virtual chest inventory with size 6 named "&8Your friend requests" to {_p}
		set {_page} to {_pageno}
		set {_%{_s}%::*} to page {_pageno} of {friendSystem::%{_p}%::friendRequests::*} with 44 lines 
		set {_slot} to 0
		loop {_%{_s}%::*}:
			set {_loop-value} to loop-value
			if loop-value is online:
				make gui slot {_slot} of {_p} with skull of "%loop-value%" parsed as offlineplayer named "&3%loop-value%" with lore "||&aPosition: &f&lOnline||&6Click to accept or deny.||&f" to run:
					close {_p}'s inventory
					set {friendSystem::%{_p}%::requestAccept} to {_loop-value}
					frOpenGUI({_p}, "reqGUI", 1)
			else:
				make gui slot {_slot} of {_p} with skull of "%loop-value%" parsed as offlineplayer named "&c%loop-value%" with lore "||&aPosition: &f&lOffline||&6Click to accept or deny.||&f" to run:
					close {_p}'s inventory 
					set {friendSystem::%{_p}%::requestAccept} to {_loop-value}
					frOpenGUI({_p}, "reqGUI", 1)
			add 1 to {_slot}
		make gui slot 50 of {_p} with skull of "MHF_ArrowRight" parsed as offlineplayer named "&6Go forward" to run:
			add 1 to {_page}
			frOpenGUI({_p}, "%{_s}%", {_page})
		if {_page} is greater than 1:
			make gui slot 48 of {_p} with skull of "MHF_ArrowLeft" parsed as offlineplayer named "&6Go back" to run:
				remove 1 from {_page}
				frOpenGUI({_p}, "%{_s}%", {_page})
		make gui slot 49 of {_p} with book named "&e&lPage %{_page}%"
		make gui slot 53 of {_p} with magma cream named "&e&lGo To Main Page" to run:
			execute {_p} command "fr list"
		make gui slot 45 of {_p} with magma cream named "&e&lGo To First Page" to run:
			set {_page} to 1
			frOpenGUI({_p}, "%{_s}%", {_page})
	else if {_s} is "reqGUI":
		open virtual chest inventory with 1 rows named "&8Friend request (&e%{friendSystem::%{_p}%::requestAccept}%&8)" to {_p}
		make gui slot 3 of {_p} with emerald block named "&a&lAccept" to close then run {_p} command "fr accept %{friendSystem::%{_p}%::requestAccept}%"
		make gui slot 4 of {_p} with skull of "%{friendSystem::%{_p}%::requestAccept}%" parsed as offlineplayer named "&6%{friendSystem::%{_p}%::requestAccept}%"
		make gui slot 5 of {_p} with redstone block named "&c&lDeny" to close then run {_p} command "fr deny %{friendSystem::%{_p}%::requestAccept}%"